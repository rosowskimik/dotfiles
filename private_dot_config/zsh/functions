# creates an alias and precedes the command with
# sudo if $EUID is not zero.
function salias() {
    emulate -L zsh
    local only=0
    local multi=0
    local key val
    while getopts ":hao" opt; do
        case $opt in
        o) only=1 ;;
        a) multi=1 ;;
        h)
            printf 'usage: salias [-hoa] <alias-expression>\n'
            printf '  -h      shows this help text.\n'
            printf '  -a      replace '\'' ; '\'' sequences with '\'' ; sudo '\''.\n'
            printf '          be careful using this option.\n'
            printf '  -o      only sets an alias if a preceding sudo would be needed.\n'
            return 0
            ;;
        *)
            salias -h >&2
            return 1
            ;;
        esac
    done
    shift "$((OPTIND - 1))"

    if ((${#argv} > 1)); then
        printf 'Too many arguments %s\n' "${#argv}"
        return 1
    fi

    key="${1%%\=*}"
    val="${1#*\=}"
    if ((EUID == 0)) && ((only == 0)); then
        alias -- "${key}=${val}"
    elif ((EUID > 0)); then
        ((multi > 0)) && val="${val// ; / ; sudo }"
        alias -- "${key}=sudo ${val}"
    fi

    return 0
}

function prepend_path() {
    if test -z "$1"; then
        target="$PWD"
    else
        target=$(realpath "$1")
    fi

    if ! test -d "$target"; then
        echo "$target not found"
        return 1
    fi

    case ":$PATH:" in
    *:"$target":*) ;;
    *)
        PATH="${target}${PATH:+:$PATH}"
        ;;
    esac
    export PATH
}

function append_path() {
    if test -z "$1"; then
        target="$PWD"
    else
        target=$(realpath "$1")
    fi

    if ! test -d "$target"; then
        echo "$target not found"
        return 1
    fi

    case ":$PATH:" in
    *:"$target":*) ;;
    *)
        PATH="${PATH:+$PATH:}${target}"
        ;;
    esac
    export PATH
}

function clzg() {
    export LAZYGIT_NEW_DIR_FILE=$XDG_STATE_HOME/lazygit/newdir

    cwd="$(pwd)"
    lazygit "$@"

    if [ -f $LAZYGIT_NEW_DIR_FILE ]; then
        newdir="$(cat $LAZYGIT_NEW_DIR_FILE)"
        if [ $cwd != $newdir ]; then
            cd "$newdir"
        fi
        rm -f $LAZYGIT_NEW_DIR_FILE >/dev/null
    fi
}

function y() {
    local tmp="$(mktemp -t "yazi-cwd.XXXXXX")" cwd
    yazi "$@" --cwd-file="$tmp"
    if cwd="$(command cat -- "$tmp")" && [ -n "$cwd" ] && [ "$cwd" != "$PWD" ]; then
        builtin cd -- "$cwd"
    fi
    rm -f -- "$tmp"
}

function leases() {
    # Check if an interface name was provided as an argument.
    if [[ -z $1 ]]; then
        # Print usage instructions to standard error and exit.
        echo "Usage: show-leases <interface>" >&2
        return 1
    fi

    # Use a local variable for the interface name for good practice.
    local interface="$1"
    networkctl --json=short status "$interface" | jq -r '
    def bytehex:
      [(./16 | floor), .%16] | map(if . < 10 then 48 + . else . + 55 end) | implode;
    (.DHCPServer.Leases // []) as $leases |
    if ($leases | length > 0) then
      (
        $leases |
        map(
          "Hostname: \(.Hostname)\nMAC: \(.HardwareAddress[:6] | map(bytehex) | join(":"))\nIP: \(.Address | join("."))"
        ) |
        join("\n\n")
      )
    else
      "No Leases"
    end
  '
}

function disasm() {
    gdb --batch --nx -ex "disas/r ${2}" "$1"
}

## END OF FILE #################################################################
# vim:filetype=zsh foldmethod=marker autoindent expandtab shiftwidth=4
# Local variables:
# mode: sh
# End:
