# creates an alias and precedes the command with
# sudo if $EUID is not zero.
function salias() {
    emulate -L zsh
    local only=0
    local multi=0
    local key val
    while getopts ":hao" opt; do
        case $opt in
        o) only=1 ;;
        a) multi=1 ;;
        h)
            printf 'usage: salias [-hoa] <alias-expression>\n'
            printf '  -h      shows this help text.\n'
            printf '  -a      replace '\'' ; '\'' sequences with '\'' ; sudo '\''.\n'
            printf '          be careful using this option.\n'
            printf '  -o      only sets an alias if a preceding sudo would be needed.\n'
            return 0
            ;;
        *)
            salias -h >&2
            return 1
            ;;
        esac
    done
    shift "$((OPTIND - 1))"

    if ((${#argv} > 1)); then
        printf 'Too many arguments %s\n' "${#argv}"
        return 1
    fi

    key="${1%%\=*}"
    val="${1#*\=}"
    if ((EUID == 0)) && ((only == 0)); then
        alias -- "${key}=${val}"
    elif ((EUID > 0)); then
        ((multi > 0)) && val="${val// ; / ; sudo }"
        alias -- "${key}=sudo ${val}"
    fi

    return 0
}

function prepend_path() {
    if test -z "$1"; then
        target="$PWD"
    else
        target=$(realpath "$1")
    fi

    if ! test -d "$target"; then
        echo "$target not found"
        return 1
    fi

    case ":$PATH:" in
    *:"$target":*) ;;
    *)
        PATH="${target}${PATH:+:$PATH}"
        ;;
    esac
    export PATH
}

function append_path() {
    if test -z "$1"; then
        target="$PWD"
    else
        target=$(realpath "$1")
    fi

    if ! test -d "$target"; then
        echo "$target not found"
        return 1
    fi

    case ":$PATH:" in
    *:"$target":*) ;;
    *)
        PATH="${PATH:+$PATH:}${target}"
        ;;
    esac
    export PATH
}

function lzg() {
    export LAZYGIT_NEW_DIR_FILE=$XDG_STATE_HOME/lazygit/newdir

    cwd="$(pwd)"
    lazygit "$@"

    if [ -f $LAZYGIT_NEW_DIR_FILE ]; then
        newdir="$(cat $LAZYGIT_NEW_DIR_FILE)"
        if [ $cwd != $newdir ]; then
            cd "$newdir"
        fi
        rm -f $LAZYGIT_NEW_DIR_FILE >/dev/null
    fi
}

function y() {
    local tmp="$(mktemp -t "yazi-cwd.XXXXXX")" cwd
    yazi "$@" --cwd-file="$tmp"
    if cwd="$(command cat -- "$tmp")" && [ -n "$cwd" ] && [ "$cwd" != "$PWD" ]; then
        builtin cd -- "$cwd"
    fi
    rm -f -- "$tmp"
}

function disasm() {
    gdb --batch --nx -ex "disas/r ${2}" "$1"
}

function _disasm_complete() {
    if ((${#words[@]} < 3)); then
        _files
    else
        compadd $(readelf -sW ${words[2]} | awk '$4 == "FUNC"' | grep -oE "[_a-zA-Z][_a-zA-Z0-9]*$")
    fi
}

if [ -n "$ZSH_VERSION" ]; then
    compdef _disasm_complete disasm
fi

## END OF FILE #################################################################
# vim:filetype=zsh foldmethod=marker autoindent expandtab shiftwidth=4
# Local variables:
# mode: sh
# End:
